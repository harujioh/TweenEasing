apply plugin: 'java'
apply plugin: 'com.diffplug.gradle.spotless'
apply plugin: 'maven'
apply plugin: 'eclipse'

def myAppVersion = '1.0.0'

group = 'jp.harujioh'
version = myAppVersion
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
}

configurations {
    imageCompile
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'org.slf4j:slf4j-api:1.7.25'

    // Image
    imageCompile 'org.jfree:jfreechart:1.0.19'
}

sourceSets {
    image {
        java {
            srcDirs = ['src/image/java']
        }
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

eclipse {
    classpath {
        plusConfigurations += [configurations.imageCompile]
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.diffplug.gradle.spotless:spotless:1.3.3'
    }
}

javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
}

task image (type: JavaExec) {
    main = 'jp.harujioh.easing.CreateSampleImage'
    classpath = sourceSets.image.runtimeClasspath

    def imageDir = new File("${buildDir}/image")
    if(!imageDir.isDirectory()){
        imageDir.mkdirs();
    }
    args imageDir, 150, 100
}

jar {
    from sourceSets.main.allSource
}

spotless {
    java {
        eclipseFormatFile 'spotless.eclipseformat.xml'
    }
}

ext {
    mavenRepository = file('maven')
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:${projectDir}/releases")
        }
    }
}

[install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
    pom.project {
        inceptionYear '2016'
        packaging 'jar'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
    }
}
